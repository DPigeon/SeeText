apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147" // For CD pipeline
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.seetext"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode getVersionCode()
        versionName getVersionName()
        // To improve image processing performance
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Don't compress included TensorFlow Lite models on build.
    aaptOptions {
        noCompress "tflite"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    lintOptions {
        disable "UnsafeExperimentalUsageError",
                "UnsafeExperimentalUsageWarning"
    }
}

def getVersionCode() {
    def stdOut = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--all', '--count'
        standardOutput = stdOut
    }
    return stdOut.toString().split("\n").size()
}

def getVersionName() {
    def stdOut = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--abbrev=0'
        standardOutput = stdOut
    }
    return stdOut.toString().trim()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // AppIntro slides
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    // ShowcaseView Guide Tour
    implementation 'com.github.mreram:showcaseview:1.1'

    // Loading overlay
    implementation 'com.pnikosis:materialish-progress:1.7'

    // CameraX core library using the camera2 implementation
    def cameraX_version = "1.0.0-beta07"
    implementation "androidx.camera:camera-camera2:${cameraX_version}"
    // If you want to additionally use the CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:${cameraX_version}"
    // If you want to additionally use the CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha14"
    // If you want to additionally use the CameraX Extensions library
    implementation "androidx.camera:camera-extensions:1.0.0-alpha14"

    // Firebase ML Face Detection
    implementation 'com.google.firebase:firebase-ml-vision:24.0.3'

    // Firebase ML Translation
    implementation 'com.google.firebase:firebase-ml-natural-language:22.0.0'
    implementation 'com.google.firebase:firebase-ml-natural-language-translate-model:20.0.8'
    implementation 'com.google.firebase:firebase-ml-natural-language-language-id-model:20.0.7'

    // Fritz AI Image Labeler
    implementation 'ai.fritz:core:6.0.1'
    implementation 'ai.fritz:vision:6.0.1'
    implementation 'ai.fritz:vision-object-detection-model-fast:3.0.0'
    // Adds the TensorFlow Lite model uncompressed in the app (increases size)

    // Material icons
    implementation 'com.google.android.material:material:1.2.0'
}
